import net.minecraftforge.snowblower.gradle.DependencyHashingTask

plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.minecraftforge.gradleutils' version '2.+'
}

group 'net.minecraftforge'
version = gradleutils.getTagOffsetVersion()
println('Version: ' + version)

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()

    maven gradleutils.forgeMaven
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    // When updating shade dependencies, you must also run the generateDependencyHashes task
    shade 'org.eclipse.jgit:org.eclipse.jgit:6.4.0.202211300538-r'
    shade 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    shade 'com.google.code.gson:gson:2.10.1'
    shade 'net.minecraftforge:srgutils:0.4.13'
    shade 'net.minecraftforge:mergetool:1.1.5'
    shade 'net.minecraftforge:ForgeAutoRenamingTool:0.1.24'
    shade 'net.minecraftforge:forgeflower:2.0.605.3'
}

tasks.register('generateDependencyHashes', DependencyHashingTask) {
    configuration = configurations.shade
    output = file('src/main/generated_resources/dependency_hashes.txt')
}

sourceSets {
    main {
        resources {
            srcDirs += file('src/main/generated_resources')
        }
    }
}

processResources {
    dependsOn tasks.named('generateDependencyHashes')
    from 'gradlew'
    from 'gradlew.bat'
    from 'gradle'
}

eclipseClasspath.dependsOn('generateDependencyHashes')
tasks.idea.dependsOn('generateDependencyHashes')

jar {
    manifest {
        attributes([
            'Main-Class': 'net.minecraftforge.snowblower.Main',
            'Implementation-Version': "${project.version} (${gradleutils.gitInfo.commit})"
        ])
    }
}

shadowJar {
    archiveClassifier = 'all'
    configurations = [project.configurations.shade]

    minimize()
}

artifacts {
    archives shadowJar
}

license {
    header = file('HEADER')
    ext.fullname = 'Forge Development LLC'
    exclude '**/*.properties'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}